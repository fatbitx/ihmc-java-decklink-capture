apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'application'

sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.1.5'


mainClassName = "us.ihmc.javadecklink.Capture"

repositories {
        maven {
                url "https://bengal.ihmc.us/nexus/content/repositories/releases/"
        }

    /**
     * Maven central repository, local proxy
     */
    maven{
        url "https://bengal.ihmc.us/nexus/content/repositories/central/"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}



sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['resources']
		}
	}
}

dependencies
{
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile group: 'us.ihmc', name: 'IHMCNativeLibraryLoader', version: '1.0'
    compile group: 'us.ihmc', name: 'IHMCVideoCodecs', version: '2.0.11-b2'
}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

jar {
   manifest {
      attributes(
      "Created-By": "IHMC Gradle Build Script",
      "Implementation-Title": project.name,
      "Implementation-Version": project.version,
      "Implementation-Vendor": "IHMC",
      
      "Bundle-Name": project.name,
      "Bundle-Version": project.version,
      "Bundle-License": "http://www.apache.org/licenses/LICENSE-2.0.txt",
      "Bundle-Vendor": "IHMC")
   }
}

publishing {
	publications {
		mavenJava(MavenPublication) {

			groupId 'us.ihmc'
			artifactId 'JavaDecklinkAPI'
			version "$version"
			from components.java
			
			pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'IHMCRosControl'
                    url 'https://stash.ihmc.us/projects/IHD/repos/java-decklink-api/'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jsmith'
                            name 'Jesper Smith'
                            email 'jsmith@ihmc.us'
                        }
                    }
                }

        	}
			
			artifact sourceJar {
				classifier "sources"
			}
		}

	}

	repositories {
		maven {
			credentials {
				username project.hasProperty('nexus_username')?"${nexus_username}":"invalid"
				password project.hasProperty('nexus_password')?"${nexus_password}":"invalid"
			}
			url "https://bengal.ihmc.us/nexus/content/repositories/releases"
		}
	}
}
